/*
 * For more details take a look at the Java Quickstart chapter in the Gradle
 * user guide available at https://docs.gradle.org/3.4.1/userguide/tutorial_java_projects.html
 */
import org.gradle.internal.os.OperatingSystem

// Apply the java plugin to add support for Java
apply plugin: 'java'

// Apply the application plugin to add support for building an application
apply plugin: 'application'

// Required for running GLFW on Mac - create window only on first thread
if ( OperatingSystem.current() == OperatingSystem.MAC_OS) {
    applicationDefaultJvmArgs = ["-XstartOnFirstThread"]
}

// In this section you declare where to find the dependencies of your project
repositories {
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
    maven {
        url "http://dl.bintray.com/lambda-engine/lambda"
    }
}

// https://github.com/tacocat/lambda/issues/18
// This hack pulls in operating system specific natives
switch ( OperatingSystem.current() ) {
    case OperatingSystem.WINDOWS:
        project.ext.lwjglNatives = "natives-windows"
        break
    case OperatingSystem.LINUX:
        project.ext.lwjglNatives = "natives-linux"
        break
    case OperatingSystem.MAC_OS:
        project.ext.lwjglNatives = "natives-macos"
        break
}

project.ext.lwjglVersion = "3.1.1"

dependencies {
    compile 'com.tacocat:lambda:0.+'

    // Hack to load natives
    runtime "org.lwjgl:lwjgl:${lwjglVersion}:${lwjglNatives}"
    runtime "org.lwjgl:lwjgl-glfw:${lwjglVersion}:${lwjglNatives}"
    runtime "org.lwjgl:lwjgl-openal:${lwjglVersion}:${lwjglNatives}"
    runtime "org.lwjgl:lwjgl-opengl:${lwjglVersion}:${lwjglNatives}"
}

// Define the main class for the application
mainClassName = 'GameApp'

